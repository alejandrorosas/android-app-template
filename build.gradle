buildscript {
    ext.kotlin_version = "1.7.10"
    ext.dagger_version = "2.43.2"
    ext.compose_version = "1.2.1"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.3.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_version"
    }
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"

    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
            allWarningsAsErrors = true
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 32
                buildToolsVersion "32.0.0"

                defaultConfig {
                    minSdkVersion 24
                    targetSdkVersion 32

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11
                }

                buildFeatures {
                    compose true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion "1.3.0"
                }

                packagingOptions {
                    exclude "META-INF/AL2.0"
                    exclude "META-INF/LGPL2.1"
                }
            }
        }
    }
}

apply from: "dependencies.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}
