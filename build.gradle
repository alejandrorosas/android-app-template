buildscript {
    ext.kotlin_version = "1.5.31"
    ext.dagger_version = "2.40.3"
    ext.compose_version = "1.0.5"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.0.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_version"
    }
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"

    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
            allWarningsAsErrors = true
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 31
                buildToolsVersion "30.0.3"

                defaultConfig {
                    minSdkVersion 23
                    targetSdkVersion 31

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildFeatures {
                    compose true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion rootProject.compose_version
                }

                packagingOptions {
                    exclude "META-INF/AL2.0"
                    exclude "META-INF/LGPL2.1"
                }
            }
        }
    }
}

apply from: "dependencies.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}
