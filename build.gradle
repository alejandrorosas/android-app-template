buildscript {
    ext.kotlin_version = "1.4.30"
    ext.dagger_version = "2.33"
    ext.hilt_version = "$dagger_version-beta"
    ext.compose_version = "1.0.0-beta01"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.0.0-alpha08"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"

    repositories {
        google()
        mavenCentral()

        maven {
            url "https://kotlin.bintray.com/kotlinx"
            content {
                // TODO: Deprecate jcenter/bintray (https://github.com/Kotlin/kotlinx.collections.immutable/issues/96)
                //       Transitive from androidx.compose.ui:ui:1.0.0-alpha11
                includeModule("org.jetbrains.kotlinx", "kotlinx-collections-immutable-jvm")
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            useIR = true
            allWarningsAsErrors = true
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 30
                buildToolsVersion "30.0.3"

                defaultConfig {
                    minSdkVersion 23
                    targetSdkVersion 30
                }

                buildFeatures {
                    compose true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion rootProject.compose_version
                }
            }
        }
    }
}

apply from: "dependencies.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}
