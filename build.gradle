buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath libs.android.gradle.plugin
        classpath libs.kotlin.gradle.plugin
        classpath libs.dagger.gradle.plugin
    }
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"

    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
            allWarningsAsErrors = true
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 33

                defaultConfig {
                    minSdkVersion 26
                    targetSdkVersion 33

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11
                }

                buildFeatures {
                    compose true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
                }

                packagingOptions {
                    exclude "META-INF/AL2.0"
                    exclude "META-INF/LGPL2.1"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
