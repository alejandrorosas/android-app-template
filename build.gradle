buildscript {
    ext.kotlin_version = '1.4.21-2'
    ext.dagger_version = '2.31.2'
    ext.hilt_version = "$dagger_version-alpha"
    ext.compose_version = "1.0.0-alpha11"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.0.0-alpha05"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"

    repositories {
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            useIR = true
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion 30
                buildToolsVersion "30.0.3"

                defaultConfig {
                    minSdkVersion 23
                    targetSdkVersion 30

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildFeatures {
                    compose true
                }

                composeOptions {
                    kotlinCompilerVersion rootProject.kotlin_version
                    kotlinCompilerExtensionVersion rootProject.compose_version
                }
            }
        }
    }
}

apply from: 'dependencies.gradle'

task clean(type: Delete) {
    delete rootProject.buildDir
}
